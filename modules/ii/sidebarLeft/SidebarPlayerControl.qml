pragma ComponentBehavior: Bound
import qs.modules.common
import qs.modules.common.models
import qs.modules.common.widgets
import qs.services
import qs.modules.common.functions
import Qt5Compat.GraphicalEffects
import QtQuick
import QtQuick.Controls
import QtQuick.Effects
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Services.Mpris

Item {
    id: root
    readonly property var players: MprisController.players
    property var player: MprisController.activePlayer ?? root.players[0] ?? null
    readonly property bool lyricsUnavailable: LyricsService.status === "not_found"
        || LyricsService.status === "no_info"
    readonly property bool showLyrics: (Config.options.sidebar.media.showLyrics ?? true)
        && !root.lyricsUnavailable
    property color artDominantColor: Config.options.sidebar.media.artColors
        ? ColorUtils.mix(
            (colorQuantizer?.colors[0] ?? Appearance.colors.colPrimary),
            Appearance.colors.colPrimaryContainer,
            0.8
          )
        : Appearance.colors.colPrimaryContainer
    property list<real> visualizerPoints: []
    property real maxVisualizerValue: 1000
    property int visualizerSmoothing: 2
    property real radius
    property bool blurredBackground: Config.options.sidebar.media.blurredBackground ?? false
    property bool shapeArt: Config.options.sidebar.media.shapeArt ?? false
    readonly property var artShapeOptions: ["Circle", "Square", "Pill", "Bun", "Cookie12Sided", "Clover4Leaf", "Heart", "Slanted", "Arch", "Arrow", "SemiCircle", "Oval", "Triangle", "Diamond", "ClamShell", "Pentagon", "Gem", "Sunny", "VerySunny", "Cookie4Sided", "Cookie6Sided", "Cookie7Sided", "Cookie9Sided", "Ghostish", "Clover8Leaf", "Burst", "SoftBurst", "Boom", "SoftBoom", "Flower", "Puffy", "PuffyDiamond"]

    readonly property string displayedArtFilePath: MediaArtwork.source

    Timer {
        running: root.player?.playbackState == MprisPlaybackState.Playing
        interval: Config.options.resources.updateInterval
        repeat: true
        onTriggered: root.player?.positionChanged()  
    }

    ColorQuantizer {
        id: colorQuantizer
        source: root.displayedArtFilePath
        depth: 0
        rescaleSize: 1
    }

    property QtObject blendedColors: AdaptedMaterialScheme {
        color: artDominantColor
    }

    Rectangle {
        id: background
        anchors.fill: parent
        anchors.leftMargin: 4
        anchors.rightMargin: 4
        anchors.topMargin: -1
        anchors.bottomMargin: 4
        color: ColorUtils.transparentize(artDominantColor, 0.9)
        radius: Appearance.rounding.normal
        clip: true

        Image {
            id: blurArtSource
            anchors.fill: parent
            source: root.displayedArtFilePath
            fillMode: Image.PreserveAspectCrop
            cache: false
            asynchronous: true
            visible: false
        }

        FastBlur {
            id: blurArt
            anchors.fill: parent
            source: blurArtSource
            radius: 80
            opacity: 0.5
            visible: root.blurredBackground && root.displayedArtFilePath !== ""

            layer.enabled: blurArt.visible
            layer.effect: OpacityMask {
                maskSource: Rectangle {
                    width: blurArt.width
                    height: blurArt.height
                    radius: background.radius
                }
            }
        }

        ColumnLayout {
            anchors.fill: parent
            anchors.margins: parent.height * 0.04
            spacing: 0

            // ── Album art ──
            Item {
                id: artBackground
                Layout.alignment: Qt.AlignHCenter
                Layout.preferredWidth: Math.min(parent.width * 1, parent.height * 0.45)
                Layout.preferredHeight: Layout.preferredWidth

                property bool useShape: root.shapeArt && (Config.options.sidebar.media.artShape ?? "Rectangle") !== "Rectangle"
                property int materialShape: ShapeUtils.getShape(Config.options.sidebar.media.artShape)

                Rectangle {
                    id: artRect
                    anchors.fill: parent
                    visible: !artBackground.useShape
                    radius: Appearance.rounding.normal
                    color: Appearance.colors.colPrimaryContainer

                    layer.enabled: !artBackground.useShape
                    layer.effect: OpacityMask {
                        maskSource: Rectangle {
                            width: artRect.width
                            height: artRect.height
                            radius: artRect.radius
                        }
                    }

                    StyledImage {
                        anchors.fill: parent
                        source: root.displayedArtFilePath
                        fillMode: Image.PreserveAspectCrop
                        cache: false
                        antialiasing: true
                        sourceSize.width: artBackground.width * 2
                        sourceSize.height: artBackground.height * 2
                    }
                }

                MaterialShape {
                    id: artShapeItem
                    anchors.fill: parent
                    visible: artBackground.useShape
                    shape: artBackground.materialShape
                    color: Appearance.colors.colPrimaryContainer

                    layer.enabled: artBackground.useShape
                    layer.effect: OpacityMask {
                        maskSource: MaterialShape {
                            width: artShapeItem.width
                            height: artShapeItem.height
                            shape: artBackground.materialShape
                        }
                    }

                    StyledImage {
                        anchors.fill: parent
                        source: root.displayedArtFilePath
                        fillMode: Image.PreserveAspectCrop
                        cache: false
                        antialiasing: true
                        sourceSize.width: artBackground.width * 2
                        sourceSize.height: artBackground.height * 2
                    }
                }

                MaterialSymbol {
                    visible: root.displayedArtFilePath === ""
                    anchors.centerIn: parent 
                    fill: 1
                    text: "music_note"
                    color: Appearance.colors.colPrimary
                    iconSize: Appearance.font.pixelSize.hugeass + 100
                }

                MouseArea {
                    anchors.fill: parent
                    enabled: root.player !== null
                    cursorShape: Qt.PointingHandCursor
                    onClicked: MprisController.raiseActivePlayer()
                }
            }

            // ── Title & Artist ──
            ColumnLayout {
                Layout.fillWidth: true
                Layout.topMargin: 20
                spacing: 5

                Item {
                    Layout.fillWidth: true
                    Layout.preferredHeight: titleText.implicitHeight
                    clip: true

                    StyledText {
                        id: titleText
                        anchors.verticalCenter: parent.verticalCenter
                        width: parent.width
                        font.pixelSize: Appearance.font.pixelSize.huge
                        font.weight: Font.Bold
                        color: blendedColors.colOnLayer0
                        horizontalAlignment: Text.AlignHCenter
                        verticalAlignment: Text.AlignVCenter
                        elide: Text.ElideRight
                        text: StringUtils.cleanMusicTitle(MprisController.displayTrackTitle) || "Play"

                        Behavior on text {
                            SequentialAnimation {
                                NumberAnimation { target: titleText; property: "x"; to: -titleText.width; duration: 150; easing.type: Easing.InQuad }
                                PropertyAction { target: titleText; property: "text" }
                                NumberAnimation { target: titleText; property: "x"; from: titleText.width; to: 0; duration: 150; easing.type: Easing.OutQuad }
                            }
                        }
                    }

                    MouseArea {
                        anchors.fill: parent
                        enabled: root.player !== null
                        cursorShape: Qt.PointingHandCursor
                        onClicked: MprisController.raiseActivePlayer()
                    }
                }

                Item {
                    Layout.fillWidth: true
                    Layout.preferredHeight: artistText.implicitHeight
                    clip: true

                    StyledText {
                        id: artistText
                        anchors.verticalCenter: parent.verticalCenter
                        width: parent.width
                        font.pixelSize: Appearance.font.pixelSize.large 
                        color: blendedColors.colSubtext
                        horizontalAlignment: Text.AlignHCenter
                        verticalAlignment: Text.AlignVCenter
                        elide: Text.ElideRight
                        text: MprisController.displayTrackArtist || "Something"

                        Behavior on text {
                            SequentialAnimation {
                                NumberAnimation { target: artistText; property: "x"; to: -artistText.width; duration: 150; easing.type: Easing.InQuad }
                                PropertyAction { target: artistText; property: "text" }
                                NumberAnimation { target: artistText; property: "x"; from: artistText.width; to: 0; duration: 150; easing.type: Easing.OutQuad }
                            }
                        }
                    }

                    MouseArea {
                        anchors.fill: parent
                        enabled: root.player !== null
                        cursorShape: Qt.PointingHandCursor
                        onClicked: MprisController.raiseActivePlayer()
                    }
                }
            }

            // ── Lyrics ──
            Item {
                Layout.fillWidth: true
                Layout.fillHeight: true

                Lyrics {
                    id: lyricsComp
                    anchors.fill: parent
                    opacity: MprisController.activePlayer !== null && root.showLyrics ? 1 : 0
                    textAlignment: Text.AlignHCenter
                    textColor: blendedColors.colOnLayer0
                    activeColor: blendedColors.colPrimary
                    dimColor: blendedColors.colSubtext
                    indicatorColor: {
                        let c = blendedColors.colPrimaryContainer
                        return (c && c != "#000000" && c != "transparent") ? c : root.artDominantColor
                    }
                    indicatorShapeColor: {
                        let c = blendedColors.colOnPrimaryContainer
                        if (c && c != "#000000" && c != "#ffffff" && c != "transparent") return c
                        return blendedColors.colPrimary || Appearance.colors.colPrimary
                    }
                    Behavior on opacity { NumberAnimation { duration: 200 } }
                }

                Loader {
                    anchors.fill: parent
                    active: !root.showLyrics
                    opacity: active ? 1 : 0
                    Behavior on opacity { NumberAnimation { duration: 200 } }

                    sourceComponent: Visualizer {
                        vertical: false
                        mirrored: false
                        barCount: 32
                        dotSize: 5
                        dotSpacing: 6
                        maxBarHeight: parent.height * 0.8
                        frameSmoothing: true
                    }
                }
            }

            // ── Controls ──
            ColumnLayout {
                Layout.fillWidth: true
                Layout.topMargin: 20
                spacing: 20

                RowLayout {
                    Layout.fillWidth: true
                    spacing: 24

                    RippleButton {
                        property real baseSize: Math.max(70, parent.parent.height * 0.1)
                        Layout.fillWidth: true
                        implicitHeight: baseSize
                        buttonRadius: (root.player?.isPlaying ?? false) ? Appearance.rounding.verylarge : baseSize / 2
                        colBackground: (root.player?.isPlaying ?? false) ? blendedColors.colPrimary : blendedColors.colSecondaryContainer
                        colBackgroundHover: (root.player?.isPlaying ?? false) ? blendedColors.colPrimaryHover : blendedColors.colSecondaryContainerHover
                        colRipple: (root.player?.isPlaying ?? false) ? blendedColors.colPrimaryActive : blendedColors.colSecondaryContainerActive
                        downAction: () => MprisController.togglePlaying()
                        contentItem: MaterialSymbol {
                            iconSize: 50
                            fill: 1
                            horizontalAlignment: Text.AlignHCenter
                            color: (root.player?.isPlaying ?? false) ? blendedColors.colOnPrimary : blendedColors.colOnSecondaryContainer
                            text: (root.player?.isPlaying ?? false) ? "pause" : "play_arrow"
                            Behavior on color {
                                animation: Appearance.animation.elementMoveFast.colorAnimation.createObject(this)
                            }
                        }
                    }

                    RippleButton {
                        property real baseSize: Math.max(60, parent.parent.height * 0.06)
                        implicitWidth: baseSize
                        implicitHeight: baseSize 
                        buttonRadius: Appearance.rounding.verylarge
                        colBackground: "transparent"
                        colBackgroundHover: "transparent"
                        colRipple: "transparent"
                        padding: -10
                        downAction: () => root.player?.next()
                        contentItem: MaterialShapeWrappedMaterialSymbol {
                            wrappedShape: MaterialShape.Shape.Cookie12Sided
                            padding: 0
                            iconSize: 32
                            fill: 1
                            text: "skip_next"
                            color: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                            colSymbol: blendedColors.colOnSecondaryContainer
                        }
                    }
                }

                RowLayout {
                    Layout.fillWidth: true
                    spacing: 24

                    RippleButton {
                        property real baseSize: Math.max(60, parent.parent.height * 0.06)
                        implicitWidth: baseSize
                        implicitHeight: baseSize 
                        buttonRadius: Appearance.rounding.verylarge
                        colBackground: "transparent"
                        colBackgroundHover: "transparent"
                        colRipple: "transparent"
                        padding: -10
                        downAction: () => root.player?.previous()
                        contentItem: MaterialShapeWrappedMaterialSymbol {
                            wrappedShape: MaterialShape.Shape.Cookie12Sided
                            padding: 0
                            iconSize: 32
                            fill: 1
                            text: "skip_previous"
                            color: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                            colSymbol: blendedColors.colOnSecondaryContainer
                        }
                    }

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 4

                        Item {
                            Layout.fillWidth: true
                            implicitHeight: Math.max(sliderLoader.implicitHeight, progressBarLoader.implicitHeight)

                            Loader {
                                id: sliderLoader
                                property var player: root.player
                                anchors.fill: parent
                            active: (sliderLoader.player?.canSeek ?? false)
                                && MediaArtwork.durationKnown
                                sourceComponent: StyledSlider {
                                    configuration: StyledSlider.Configuration.Wavy
                                    highlightColor: blendedColors.colPrimary
                                    trackColor: blendedColors.colSecondaryContainer
                                    handleColor: blendedColors.colPrimary
                                    value: MediaArtwork.durationKnown
                                        ? MediaArtwork.playbackPosition / MediaArtwork.playbackLength : 0
                                    onMoved: {
                                        sliderLoader.player.position = MediaArtwork.rawPositionFor(
                                            value * MediaArtwork.playbackLength)
                                        lyricsComp.restartLyrics()
                                    }
                                }
                            }

                            Loader {
                                id: progressBarLoader
                                property var player: root.player
                                anchors {
                                    verticalCenter: parent.verticalCenter
                                    left: parent.left
                                    right: parent.right
                                }
                                active: !(progressBarLoader.player?.canSeek ?? false)
                                sourceComponent: StyledProgressBar {
                                    wavy: progressBarLoader.player?.isPlaying ?? false
                                    highlightColor: blendedColors.colPrimary
                                    trackColor: blendedColors.colSecondaryContainer
                                    value: MediaArtwork.durationKnown
                                        ? MediaArtwork.playbackPosition / MediaArtwork.playbackLength : 0
                                }
                            }
                        }

                        RowLayout {
                            Layout.fillWidth: true

                            StyledText {
                                font.pixelSize: Appearance.font.pixelSize.normal
                                color: blendedColors.colSubtext
                                font.letterSpacing: -0.4
                                font.features: { "tnum": 1 }
                                text: StringUtils.friendlyTimeForSeconds(MediaArtwork.playbackPosition)
                            }

                            Item { Layout.fillWidth: true }

                            StyledText {
                                font.pixelSize: Appearance.font.pixelSize.normal
                                color: blendedColors.colSubtext
                                font.letterSpacing: -0.4
                                font.features: { "tnum": 1 }
                                text: MediaArtwork.durationKnown
                                    ? StringUtils.friendlyTimeForSeconds(MediaArtwork.playbackLength)
                                    : "--:--"
                            }
                        }
                    }
                }
            }

            // ── Lyrics toggle Volume & settings  ──
            RowLayout {
                Layout.fillWidth: true
                Layout.topMargin: 20
                spacing: 8

                RippleButton {
                    property real baseSize: Math.max(36, parent.parent.height * 0.05)
                    implicitWidth: baseSize
                    implicitHeight: baseSize
                    buttonRadius: Appearance.rounding.large
                    colBackground: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                    colBackgroundHover: blendedColors.colSecondaryContainerHover
                    colRipple: blendedColors.colSecondaryContainerActive
                    downAction: () => {
                        Config.options.sidebar.media.showLyrics = !Config.options.sidebar.media.showLyrics
                    }
                    contentItem: MaterialSymbol {
                        iconSize: 18
                        fill: root.showLyrics ? 1 : 0
                        horizontalAlignment: Text.AlignHCenter
                        color: blendedColors.colOnSecondaryContainer
                        text: "lyrics"
                    }
                }

                RippleButton {
                    property real baseSize: Math.max(36, parent.parent.height * 0.05)
                    implicitWidth: baseSize
                    implicitHeight: baseSize
                    buttonRadius: Appearance.rounding.large
                    colBackground: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                    colBackgroundHover: blendedColors.colSecondaryContainerHover
                    colRipple: blendedColors.colSecondaryContainerActive
                    downAction: () => {
                        if (root.player) root.player.volume = (root.player.volume > 0) ? 0 : 1.0  
                    }
                    contentItem: MaterialSymbol {
                        iconSize: 18
                        fill: 1
                        horizontalAlignment: Text.AlignHCenter
                        color: blendedColors.colOnSecondaryContainer
                        text: (root.player?.volume ?? 1) <= 0 ? "volume_off"
                            : (root.player?.volume ?? 1) < 0.5 ? "volume_down"
                            : "volume_up"
                    }
                }

                RippleButton {
                    property real baseSize: Math.max(36, parent.parent.height * 0.05)
                    Layout.fillWidth: true
                    implicitHeight: baseSize
                    buttonRadius: Appearance.rounding.large
                    colBackground: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                    colBackgroundHover: blendedColors.colSecondaryContainerHover
                    colRipple: blendedColors.colSecondaryContainerActive
                    downAction: () => {
                        if (root.player) root.player.volume = Math.max(0, (root.player.volume ?? 1) - 0.1)  
                    }
                    contentItem: MaterialSymbol {
                        iconSize: 18
                        fill: 1
                        horizontalAlignment: Text.AlignHCenter
                        color: blendedColors.colOnSecondaryContainer
                        text: "volume_down"
                    }
                }

                RippleButton {
                    property real baseSize: Math.max(36, parent.parent.height * 0.05)
                    Layout.fillWidth: true
                    implicitHeight: baseSize
                    buttonRadius: Appearance.rounding.large
                    colBackground: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                    colBackgroundHover: blendedColors.colSecondaryContainerHover
                    colRipple: blendedColors.colSecondaryContainerActive
                    downAction: () => {
                        if (root.player) root.player.volume = Math.min(1.5, (root.player.volume ?? 1) + 0.1)  
                    }
                    contentItem: MaterialSymbol {
                        iconSize: 18
                        fill: 1
                        horizontalAlignment: Text.AlignHCenter
                        color: blendedColors.colOnSecondaryContainer
                        text: "volume_up"
                    }
                }

                RippleButton {
                    id: moreButton
                    property real baseSize: Math.max(36, parent.parent.height * 0.05)
                    implicitWidth: baseSize
                    implicitHeight: baseSize
                    buttonRadius: Appearance.rounding.large
                    colBackground: ColorUtils.transparentize(blendedColors.colSecondaryContainer, 0.7)
                    colBackgroundHover: blendedColors.colSecondaryContainerHover
                    colRipple: blendedColors.colSecondaryContainerActive
                    downAction: () => menuPopup.open()
                    contentItem: MaterialSymbol {
                        iconSize: 18
                        fill: 1
                        horizontalAlignment: Text.AlignHCenter
                        color: blendedColors.colOnSecondaryContainer
                        text: "more_vert"
                    }

                    Popup {
                        id: menuPopup
                        y: -implicitHeight - 8
                        x: moreButton.width - implicitWidth
                        width: 210
                        padding: 16
                        modal: true
                        dim: false
                        closePolicy: Popup.CloseOnPressOutside | Popup.CloseOnEscape

                        background: Rectangle {
                            color: Appearance.m3colors.m3surfaceContainer
                            radius: Appearance.rounding.verylarge
                            border.width: 2
                            border.color: Appearance.colors.colLayer0Border
                        }

                        contentItem: ColumnLayout {
                            width: menuPopup.width
                            spacing: 10

                            ConfigSwitch {
                                buttonIcon: "shape_line"
                                text: Translation.tr("Shape Art")
                                checked: Config.options.sidebar.media.shapeArt
                                onCheckedChanged: { Config.options.sidebar.media.shapeArt = checked }
                            }

                            ConfigSelectionShapeArray {
                                Layout.fillWidth: true
                                visible: Config.options.sidebar.media.shapeArt
                                currentValue: Config.options.sidebar.media.artShape
                                shapeColor: Appearance.colors.colPrimary
                                backgroundColor: Appearance.colors.colPrimaryContainer
                                options: root.artShapeOptions
                                onSelected: newValue => Config.options.sidebar.media.artShape = newValue
                            }

                            ConfigSwitch {
                                buttonIcon: "radio_button_partial"
                                text: Translation.tr("Art Colors")
                                checked: Config.options.sidebar.media.artColors
                                onCheckedChanged: { Config.options.sidebar.media.artColors = checked }
                            }

                            ConfigSwitch {
                                buttonIcon: "blur_on"
                                text: Translation.tr("Blurred Art")
                                checked: Config.options.sidebar.media.blurredBackground
                                onCheckedChanged: { Config.options.sidebar.media.blurredBackground = checked }
                            }
                        }
                    }
                }
            }

            // ── Player selector ──
            StyledComboBox {
                id: playerSelector
                visible: root.players.length > 1
                Layout.fillWidth: true
                Layout.topMargin: 12
                model: root.players.map(p => p.identity ?? p.desktopEntry ?? "Unknown")
                currentIndex: Math.max(0, root.players.indexOf(root.player))
                onActivated: index => MprisController.setActivePlayer(root.players[index])
            }
        }
    }
}
